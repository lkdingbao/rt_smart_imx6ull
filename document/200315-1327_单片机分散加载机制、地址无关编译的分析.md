@[TOC]  

# 单片机的分散加载机制、地址无关编译的分析  

## 一、前言  

- 前段时间写了一篇博文简单的说明了地址无关编译中的ropi和rwpi技术，简单的用一个我目前正在使用的os进行说明（基于rt-thread），部分细节没有讲清楚，因此用此篇文章来进一步介绍地址无关编译，争取能够分析透彻。  
  
## 二、单片机的分散加载机制  

- 本次仍旧以单片机为例，因为单片机我比较熟悉，再加上单片机比较低端（相比FPGA、DSP、Intel等CPU，确实比较简单）。实际上是因为我只会单片机，菜鸟本菜实锤。   
- 以下我以基于ARM Cortex-M4架构的一款国产单片机作为分析对象（大家可以类比到stm32f407，工作原因，再具体的不方便透露）。  
  
###  1.分散加载   

分散加载实际上规定了单片机（或者机器，后面不再赘述）在正式上 电执行前后，如何准备好运行时的环境，这个环境指主要指的是变量地址如何分配。  
可能说变量地址如何分配不太恰当，但是我实在是找不到一个名词来描述这个场景：**从flash或者说是rom中把变量搬移到ram空间。**  

之所以说“搬移”，是基于以下几点考虑：  

- ram是易失性的存储介质，掉电数据就会丢失，而单片机的运行环境又是随时都都会掉电的（好比stm32下程序到sram中，下载调试快速，一掉电就凉凉），因此必须先把这些数据在flash或者rom中存一份，每次上电从rom中复制一份出来，我习惯把这种复制称为“搬移”；  
- 并且这种“搬移”不是无脑的“复制”，针对数据的不同特性（如赋初始值的、无需赋初始值的、赋初始值但初始值为0，等等），这个“复制方式”是不同的，即有的是复制、有的是解压缩、有的是赋0初始化，等等。所以我觉得叫“搬移”比较且当。  
  

**以下尽可能详细的介绍一下这种“搬移过程”。另外说明一下，我不太喜欢贴图片，所以我之后大部分会以代码的形式来展示示例。**    

#### a. 首先，先生成一个.S文件，即反汇编  

**以下都是以KEIL为例！**  

- 使用KEIL自带的fromelf.exe工具（这个工具位于KEIL的安装目录下，例如：`D:\MDK-ARM V5.25\Keil_v5\ARM\ARM\ARMCC\bin`和`D:\MDK-ARM V5.25\Keil_v5\ARM\ARM\ARMCLANG\bin`，这两个为两个不同的编译链下的工具，对于Cortex-M来说，二者没有区别，详细大家可以百度：“**armcc和armclang的区别。**”）。  
- 然后在KEIL的IDE中编译链接生成一个.axf文件。  
- 使用命令：**fromelf -c !L --output @L.S**来生成.S文件（注意，在生成.axf文件时候把调试信息勾选上，这样生成的.S文件比较容易阅读，具体方法如下图所示）。关于为什么要勾选Debug Infomation大家可以去学习以下axf文件的格式内容，关于fromelf文件的用法、格式大家可以去百度一下。  
  

![生成带调试信息的axf文件](https://imgconvert.csdnimg.cn/aHR0cHM6Ly9waWNnby13YXJlaG91c2Uub3NzLWNuLWJlaWppbmcuYWxpeXVuY3MuY29tL2ltZy8yMDAzMTUtMTMyMl9rZWlsJUU2JTg4JUFBJUU1JTlCJUJFXyVFNSU4QyU4NSVFNSU5MCVBQiVFOCVCMCU4MyVFOCVBRiU5NSVFNCVCRiVBMSVFNiU4MSVBRi5wbmc?x-oss-process=image/format,png#pic_center)  
![生成.S文件](https://imgconvert.csdnimg.cn/aHR0cHM6Ly9waWNnby13YXJlaG91c2Uub3NzLWNuLWJlaWppbmcuYWxpeXVuY3MuY29tL2ltZy8yMDAzMTUtMTMyMl9rZWlsJUU2JTg4JUFBJUU1JTlCJUJFXyVFNyU5NCU5RiVFNiU4OCU5MHMlRTYlOTYlODclRTQlQkIlQjYucG5n?x-oss-process=image/format,png#pic_center)  

**如果你完全按照我的这个操作流程，你会得到一个与.axf文件同名的.S文件。**   

#### b.介绍一下上电执行过程  

**裸机下的上电过程，裸机我就不单独摆程序了，这里只是简单叙述一下流程：**  

- 上电后会触发复位电路的复位机制（本质就是一个阻容，上电的伊始电容导通，触发了一段时间的低电平，随后电容充满电，维持高电平）  
- 复位后，ARM内核的PC指针跳转到0x0000_0000执行。  
- 根据ARM手册的规定，0x0000_0000顺序放栈指针、复位中断服务函数地址，等等。  
- 设置了栈顶，取得了复位中断服务函数的地址，然后去执行（这个在启动文件中可以找到，一般以startup_xxx.s命名）。  
- 进入复位中断服务函数，会执行`__main()`函数（这个不是我们见到的`main()`函数，这个是编译器内置函数，用于调用一些初始化操作函数，然后再调用我们的main函数，交回使用权）。  
- 再`__main()`函数中，一般会有两个函数：`__scatterload()`和`__rt_entry()`，一个用于分散加载，一个用于系统库的初始化（如`__rt_lib_init()`，这个函数内实现了动态加载过程）。  
- 然后由`__rt_entry()`调用`main()`函数，跳转到我们的c语言世界。  
- 在KEIL中，还有一个骚操作：`$Sub$$main`和`$Super$$main`，这哥俩用于在`main()`函数之前调用一些操作，毕竟之前的那些操作都是编译器来处理，我们干预不了，只能干瞪眼（`$Sub$$main`和`$Super$$main`，这个在rt-thread的源码中有使用示例，这里不赘述）。  
- 以上就完成了整个上电过程。  
  
#### c.分散加载过程  

```c
!!!main
    __main
        0x000000c0:    f000f802    ....    BL       __scatterload ; 0xc8
        0x000000c4:    f000f883    ....    BL       __rt_entry ; 0x1ce
    !!!scatter
    __scatterload
    __scatterload_rt2
    __scatterload_rt2_thumb_only
        0x000000c8:    a00a        ..      ADR      r0,{pc}+0x2c ; 0xf4
        0x000000ca:    e8900c00    ....    LDM      r0,{r10,r11}
        0x000000ce:    4482        .D      ADD      r10,r10,r0
        0x000000d0:    4483        .D      ADD      r11,r11,r0
        0x000000d2:    f1aa0701    ....    SUB      r7,r10,#1
    __scatterload_null
        0x000000d6:    45da        .E      CMP      r10,r11
        0x000000d8:    d101        ..      BNE      0xde ; __scatterload_null + 8
        0x000000da:    f000f878    ..x.    BL       __rt_entry ; 0x1ce
        0x000000de:    f2af0e09    ....    ADR      lr,{pc}-7 ; 0xd7
        0x000000e2:    e8ba000f    ....    LDM      r10!,{r0-r3}
        0x000000e6:    f0130f01    ....    TST      r3,#1
        0x000000ea:    bf18        ..      IT       NE
        0x000000ec:    1afb        ..      SUBNE    r3,r7,r3
        0x000000ee:    f0430301    C...    ORR      r3,r3,#1
        0x000000f2:    4718        .G      BX       r3
    $d
        0x000000f4:    00037f6c    l...    DCD    229228
        0x000000f8:    00037f8c    ....    DCD    229260

;;中间的代码省略...

Region$$Table$$Base
        0x00038060:    00000055    U...    DCD    85
        0x00038064:    00000002    ....    DCD    2
        0x00038068:    00001b38    8...    DCD    6968
        0x0003806c:    00037f63    c...    DCD    229219
        0x00038070:    00000819    ....    DCD    2073
        0x00038074:    00001b3a    :...    DCD    6970
        0x00038078:    0000f5d0    ....    DCD    62928
        0x0003807c:    00037eeb    .~..    DCD    229099
```

上述代码直接为从`__main()`开始的过程，即跳转到`__main()`函数中，先执行`__scatterload()`函数，这里就是分散加载的过程（实际的过程可能比这个要繁琐些，繁琐在哪，后面会介绍到）。  

**以下是分析：**  

- 执行到`__main()`函数处（实际上在汇编世界里，这就是一个标号，函数和变量没有太大区别，无非都是再地址空间里的一些有特殊含义的二进制数据）。  
- 跳转执行`0x000000c0:    f000f802    ....    BL       __scatterload ; 0xc8`，大家只需要看到`BL 0xc8`就可以，其他的信息，都是些杂七杂八的信息（前面说了生成的.axf中要带有调试信息，所以这里会看到`BL       __scatterload ; 0xc8`字样，`__scatterload()` 实际上就是从.axf文件中的符号表中提取出来的）。  
- 然后执行`__scatterload()`函数（下面的代码和上一节的是一摸一样的）。  
  
```c
!!!main
    __main
        0x000000c0:    f000f802    ....    BL       __scatterload ; 0xc8
        0x000000c4:    f000f883    ....    BL       __rt_entry ; 0x1ce
    !!!scatter
    __scatterload
    __scatterload_rt2
    __scatterload_rt2_thumb_only
        0x000000c8:    a00a        ..      ADR      r0,{pc}+0x2c ; 0xf4
        0x000000ca:    e8900c00    ....    LDM      r0,{r10,r11}
        0x000000ce:    4482        .D      ADD      r10,r10,r0
        0x000000d0:    4483        .D      ADD      r11,r11,r0
        0x000000d2:    f1aa0701    ....    SUB      r7,r10,#1
    __scatterload_null
        0x000000d6:    45da        .E      CMP      r10,r11
        0x000000d8:    d101        ..      BNE      0xde ; __scatterload_null + 8
        0x000000da:    f000f878    ..x.    BL       __rt_entry ; 0x1ce
        0x000000de:    f2af0e09    ....    ADR      lr,{pc}-7 ; 0xd7
        0x000000e2:    e8ba000f    ....    LDM      r10!,{r0-r3}
        0x000000e6:    f0130f01    ....    TST      r3,#1
        0x000000ea:    bf18        ..      IT       NE
        0x000000ec:    1afb        ..      SUBNE    r3,r7,r3
        0x000000ee:    f0430301    C...    ORR      r3,r3,#1
        0x000000f2:    4718        .G      BX       r3
    $d
        0x000000f4:    00037f6c    l...    DCD    229228
        0x000000f8:    00037f8c    ....    DCD    229260

;;中间的代码省略...

Region$$Table$$Base
        0x00038060:    00000055    U...    DCD    85
        0x00038064:    00000002    ....    DCD    2
        0x00038068:    00001b38    8...    DCD    6968
        0x0003806c:    00037f63    c...    DCD    229219
        0x00038070:    00000819    ....    DCD    2073
        0x00038074:    00001b3a    :...    DCD    6970
        0x00038078:    0000f5d0    ....    DCD    62928
        0x0003807c:    00037eeb    .~..    DCD    229099
```

很清楚的看到`0x000000c8`到`0x000000d2`这块代码，实际上是获取了`Region$$Table$$Base`这个数据块的起始地址（r10是始地址，r11是末地址+1，r7是始地址-1。只不过用了重定位技术，在链接时才知道`Region$$Table$$Base`的地址，然后回来修改`$d`处的值，所以这块代码使用了相对地址偏移，简单来说就是编译的时候`$d`这里填充的0，链接时才知道了真正的值）。  
然后顺序执行到`__scatterload_null()`处，`__scatterload_nul()`l本质是一个循环，仿佛的利用`Region$$Table$$Base`中的数据来执行，为了方便理解，我写了一个相同功能的C代码：

```c
//汇编代码
//其中这里已经知道了：r10是始地址，r11是末地址+1（用于与r10一起判断结束），r7是始地址-1（实际上是一个基地址）
 __scatterload_null
        0x000000d6:    45da        .E      CMP      r10,r11
        0x000000d8:    d101        ..      BNE      0xde ; __scatterload_null + 8
        0x000000da:    f000f878    ..x.    BL       __rt_entry ; 0x1ce
        0x000000de:    f2af0e09    ....    ADR      lr,{pc}-7 ; 0xd7
        0x000000e2:    e8ba000f    ....    LDM      r10!,{r0-r3}
        0x000000e6:    f0130f01    ....    TST      r3,#1
        0x000000ea:    bf18        ..      IT       NE
        0x000000ec:    1afb        ..      SUBNE    r3,r7,r3
        0x000000ee:    f0430301    C...    ORR      r3,r3,#1
        0x000000f2:    4718        .G      BX       r3
;;中间的代码省略...
Region$$Table$$Base
        0x00038060:    00000055    U...    DCD    85
        0x00038064:    00000002    ....    DCD    2
        0x00038068:    00001b38    8...    DCD    6968
        0x0003806c:    00037f63    c...    DCD    229219
        0x00038070:    00000819    ....    DCD    2073
        0x00038074:    00001b3a    :...    DCD    6970
        0x00038078:    0000f5d0    ....    DCD    62928
        0x0003807c:    00037eeb    .~..    DCD    229099
        
//对应的C语言代码（也是伪代码。实际上，函数的入参都是放在r0~r3中的，所以一些细节c和对应的汇编对应不上）
//不妨做以下命名：start=r10，end=r11，base=r7
//之所以入参用int32_t，这里没什么讲究，我就是单纯的认为32bit的机器，地址是32bit的，可正可负而已！
void __scatterload_null(int32_t start, int32_t end, int32_t base)
{
	TRegionTable OnceOption;
	
	while(1) 
	{
		if(start == end)
		{
			break;
		}
		else
		{
			memcpy(&OnceOption, *start, 16);//16=4个寄存器的大小
			if( OnceOption.func & 0x1 )
			{
				OnceOption.func = base - OnceOption.func;
			}
			OnceOption.func( OnceOption.load_addr,
							 OnceOption.run_addr,
							 OnceOption.size 
							 base );
		 	start += 4;
		}
	}
	//这里实际上是跳转到__rt_entry()去执行
	//但在C语言里我实在没有办法描述这个过程，姑且认为__scatterload_null()函数执行完就去执行__rt_entry()
}
typedef struct
{
	int32_t 	load_addr; //加载视图，就是下载到单片机中时的那个bin文件中ram数据的地址，一般在code+ro之后排列
	int32_t		run_addr;  //运行视图，就是运行时在ram中的实际地址
	int32_t		size;
	void (*func)(int32_t load_addr, int32_t run_addr, int32_t size, int32_t base);
}TRegionTable;
TRegionTable RegionTable[] @"Region$$Table$$Base" =
{
	//注意，把除了size外的三个的bit[1:0]忽略掉，这两位只起到标识作用（后面会讲到具体原因）
	{ 85,	2,		6968,	__decompress$$rt2veneer,			},
	{ 2073,	6970,	62928,	__scatterload_zeroinit$$rt2veneer,	},
};

//load_addr和size怎么来的，看下面：
//这个也是从相同的.S文件提取的
** Section #3 'data' (SHT_PROGBITS) [SHF_ALLOC + SHF_WRITE]
    Size   : 1988 bytes (alignment 4)
    Address: 0x000380b4
** Section #4 'bss' (SHT_NOBITS) [SHF_ALLOC + SHF_WRITE]
    Size   : 62928 bytes (alignment 4)
    Address: 0x00039bec
//会发现，6968 ≠ 1988，62928 = 62928
//造成这个的原因就是数据压缩技术。打开.map文件，一目了然。
 Code (inc.  data)   	RO Data    	RW Data	  ZI Data  Debug   
 202804      13872      26752       6968      62928    1059524   Grand Totals
 202804      13872      26752       1988      62928    1059524   ELF Image Totals (compressed)
 202804      13872      26752       1988          0          0   ROM Totals
```

稍微解释一下这个转换：  

```c
//顺便介绍下Region$$Table$$Base的结构
//每16Byte为一组，分别是加载时地址，运行时地址，操作的数据个数，操作的函数
//然后根据不同的需要，构成了一个表格
//所谓不同需要，指的是：赋初始值的变量、未赋初始值的变量，然后这个可以再细分，如下所示：
//ram --- | 未赋初始值的变量 ----------------------- .bss（调用的__scatterload_zeroinit()）
//        |                  | 赋初始值全部为全0---- .bss（调用的__scatterload_zeroinit()）
//        | 赋初始值的变量 --- | 赋初始值全部为非0 --- .rw（调用的__decompress()）
//        |                  | 赋初始值杂乱无章 ---- .rw（调用的__scatterload_copy()）
//当然上述分类可能不太准确，因为__decompress()这个太特殊了，压缩与解压缩，这个东西没法说的太细

//在顺便介绍下Region$$Table$$Base怎么得到操作函数
//首先我先列这几个函数在汇编里的信息（和之前的是在一个程序里的）：
__decompress$$rt2veneer
    0x000000fc:    f0100f01    ....    TST      r0,#1
    0x00000100:    bf18        ..      IT       NE
    0x00000102:    19c0        ..      ADDNE    r0,r0,r7
    0x00000104:    f0110f01    ....    TST      r1,#1
    0x00000108:    bf18        ..      IT       NE
    0x0000010a:    19c9        ..      ADDNE    r1,r1,r7
    0x0000010c:    f0110f02    ....    TST      r1,#2
    0x00000110:    bf18        ..      IT       NE
    0x00000112:    4449        ID      ADDNE    r1,r1,r9
    0x00000114:    f0210103    !...    BIC      r1,r1,#3
!!dczerorl2
__decompress
__decompress1
    0x00000118:    440a        .D      ADD      r2,r2,r1
;;中间的代码省略...
__scatterload_zeroinit$$rt2veneer
     0x00000174:    f0100f01    ....    TST      r0,#1
     0x00000178:    bf18        ..      IT       NE
     0x0000017a:    19c0        ..      ADDNE    r0,r0,r7
     0x0000017c:    f0110f01    ....    TST      r1,#1
     0x00000180:    bf18        ..      IT       NE
     0x00000182:    19c9        ..      ADDNE    r1,r1,r7
     0x00000184:    f0110f02    ....    TST      r1,#2
     0x00000188:    bf18        ..      IT       NE
     0x0000018a:    4449        ID      ADDNE    r1,r1,r9
     0x0000018c:    f0210103    !...    BIC      r1,r1,#3
 !!handler_zi
 __scatterload_zeroinit
     0x00000190:    2300        .#      MOVS     r3,#0
     
//然后从Region$$Table$$Base中提取几个参数
		0x0003805f:    XXXXXXXX    ....    DCD    XXXXXX ;;这个！！！就是前边的r7和base，就用了这个地址值！！！
Region$$Table$$Base
        0x00038060:    00000055    U...    DCD    85 ;;特别说明一下这个load_addr，跟数据排列有关，就是个偏移
        0x00038064:    00000002    ....    DCD    2
        0x00038068:    00001b38    8...    DCD    6968
        0x0003806c:    00037f63    c...    DCD    229219 ;;和这个！！！给个标号func1
        0x00038070:    00000819    ....    DCD    2073
        0x00038074:    00001b3a    :...    DCD    6970
        0x00038078:    0000f5d0    ....    DCD    62928
        0x0003807c:    00037eeb    .~..    DCD    229099 ;;和这个！！！给个标号func2
        
//然后在__scatterload_null()中有一个用让r3=r7-r3的操作： 0x000000ec:    1afb        ..      SUBNE    r3,r7,r3
//r3就是func1和func2的地址，不妨来计算一下：
//首先是func1，0x0003805f - 0x00037f63 = 0x000000fc，是__decompress$$rt2veneer()
//然后是func2，0x0003805f - 0x00037eeb = 0x00000174，是__scatterload_zeroinit$$rt2veneer()
//当然如果没有使用地址无关编译，多少会有出入，但你总能发现什么（或者简单点，你仿真着单步执行也能得到整个执行流程）

//最后再补充一句：
//func1和func2处的这两个值（乃至Region$$Table$$Base的表示地址的那两个，load_addr和run_addr）的bit[0]和bit[1]是由特殊用途的。
//bit[0]：=1表示这个值是个偏移，需要和基地址进行处理然后再使用（或作差或相加，基地址的获取方式就是我前面说的）
//bit[1]：=1表示这个值是地址无关的，即rwpi，需要再运行时指定一个基地址，就是r9

//我当初想了很久写这个的那个大神为什么要这么做，想了很久才明白：这么写真的很通用而且好理解。可能这就是我和大神的区别。
```

- 以上就是完整的分散加载的执行过程。但是存在一种情况，就是在ram中定义一个数组，这个数组包含函数指针且被赋初始值，如下所示：  
  
```c
typedef void (*Func)( void* arg);
Func func[2] = {StartThread_Entry, StartThread_Entry};
	
int main( void )
{
	func[0](0);
	func[1](0);
	//...其他代码
}
```

上述代码是会编译通过的，以下就来分析一下这种情况（关于地址无关编译，我们在本节就简单带过，具体地址无关编译的实现在下一节详细介绍）。  

```c
//.S文件找到相应位置（可以根据.map来辅助查找）

//1.main.c中main函数对应的代码（看到了前面说过的$Super$$main操作，可见编译器的看到的main和我们看到的main不一样）
//编译器看到的main=$Sub$$main+$Super$$main+main，它把这三个组合在一起，KEIL把这三个接口都开放给我们
//其中，$Super$$main没有什么实际作用，作用就是从跳回main，而$Sub$$main和main是实打实的函数
//关于这一点，可以留意：凡是i.$Sub$$main或i.main这种带i.前缀的标号，都是从.c/.s中编译出来的函数
i.main
$Super$$main
    0x00030868:    b51c        ..      PUSH     {r2-r4,lr}
    0x0003086a:    482c        ,H      LDR      r0,[pc,#176] ; [0x3091c] = 0x1b0c
    0x0003086c:    4448        HD      ADD      r0,r0,r9
    0x0003086e:    6801        .h      LDR      r1,[r0,#0]
    0x00030870:    2000        .       MOVS     r0,#0
    0x00030872:    4788        .G      BLX      r1
    0x00030874:    4829        )H      LDR      r0,[pc,#164] ; [0x3091c] = 0x1b0c
    0x00030876:    4448        HD      ADD      r0,r0,r9
    0x00030878:    6841        Ah      LDR      r1,[r0,#4]
    0x0003087a:    2000        .       MOVS     r0,#0
    0x0003087c:    4788        .G      BLX      r1
;;省略其他代码
$d
    0x0003091c:    00001b0c    ....    DCD    6924
;;省略其他代码
    
//2.以下是main.c中形成的动态初始化代码（用于在运行时进行rwpi的最后操作：实体化）
.text
__sta__dyninit
    0x00001d40:    4803        .H      LDR      r0,[pc,#12] ; [0x1d50] = 0x21ac3
    0x00001d42:    4478        xD      ADD      r0,r0,pc
    0x00001d44:    4903        .I      LDR      r1,[pc,#12] ; [0x1d54] = 0x1b0c
    0x00001d46:    4449        ID      ADD      r1,r1,r9
    0x00001d48:    6008        .`      STR      r0,[r1,#0]
    0x00001d4a:    6048        H`      STR      r0,[r1,#4]
    0x00001d4c:    4770        pG      BX       lr
$d
    0x00001d4e:    0000        ..      DCW    0
    0x00001d50:    00021ac3    ....    DCD    137923
    0x00001d54:    00001b0c    ....    DCD    6924
;;省略其他代码

//3.如何调用的__sta__dyninit
//__main -> __rt_entry -> __rt_lib_init -> __cpp_initialize__aeabi_
//这部操作在__scatterload之后执行的
.text
__cpp_initialize__aeabi_
    0x00002598:    b570        p.      PUSH     {r4-r6,lr}
    0x0000259a:    4c06        .L      LDR      r4,[pc,#24] ; [0x25b4] = 0x35ae0
    0x0000259c:    447c        |D      ADD      r4,r4,pc
    0x0000259e:    4d06        .M      LDR      r5,[pc,#24] ; [0x25b8] = 0x35b10
    0x000025a0:    447d        }D      ADD      r5,r5,pc
    0x000025a2:    e003        ..      B        0x25ac ; __cpp_initialize__aeabi_ + 20
    0x000025a4:    6820         h      LDR      r0,[r4,#0]
    0x000025a6:    4420         D      ADD      r0,r0,r4
    0x000025a8:    4780        .G      BLX      r0
    0x000025aa:    1d24        $.      ADDS     r4,r4,#4
    0x000025ac:    42ac        .B      CMP      r4,r5
    0x000025ae:    d1f9        ..      BNE      0x25a4 ; __cpp_initialize__aeabi_ + 12
    0x000025b0:    bd70        p.      POP      {r4-r6,pc}
$d
    0x000025b2:    0000        ..      DCW    0
    0x000025b4:    00035ae0    .Z..    DCD    219872
    0x000025b8:    00035b10    .[..    DCD    219920
;;省略其他代码
.init_array
Region$$Table$$Limit
SHT$$INIT_ARRAY$$Base
    0x00038080:    fffc8161    a...    DCD    4294738273
.init_array
    0x00038084:    fffc81a1    ....    DCD    4294738337
.init_array
    0x00038088:    fffc838d    ....    DCD    4294738829
.init_array
    0x0003808c:    fffc83bd    ....    DCD    4294738877
.init_array
    0x00038090:    fffc8425    %...    DCD    4294738981
.init_array
    0x00038094:    fffc8d3d    =...    DCD    4294741309
.init_array
    0x00038098:    fffc9109    ....    DCD    4294742281
.init_array
    0x0003809c:    fffc9481    ....    DCD    4294743169
.init_array
    0x000380a0:    fffc95fd    ....    DCD    4294743549
.init_array
    0x000380a4:    fffc96fd    ....    DCD    4294743805
.init_array
    0x000380a8:    fffc9881    ....    DCD    4294744193
.init_array
    0x000380ac:    fffc9c29    )...    DCD    4294745129
.init_array
    0x000380b0:    fffc9c91    ....    DCD    4294745233	;;注意，这个就是我的main函数中的需要动态加载的数据
.init_array
SHT$$INIT_ARRAY$$Limit

//4.开始分析
a. 在编译链接期间，使用地址无关编译技术，所以导致了我们的函数实际运行的地址是不确定的，所以是无法简单的通过之前见到的分散
加载来实现数据的“搬移”来实现ram中变量初始化。只能等.bin文件加载到flash中，开始执行了才能确定。
b. 所以链接器链接了一个__cpp_initialize__aeabi_()函数（位于KEIL提供的库"../clib/arm_runtime.c"中。额外啰嗦一句，
负责分散加载的__scatter()位于库"../clib/angel/scatter.s"中，这是一个汇编文件，所以这一导致了__scatter()可以不用使
用栈，可以早早就执行，而__cpp_initialize__aeabi_()必须等到栈初始化好了才能进行，所以必须在__scatter()之后）。
c. 然后我们来看一下__cpp_initialize__aeabi_()如何执行到每一个文件中的__sta__dyninit()。
还是老套路，不嫌麻烦，再分析一次，不过这次我们直接写对应的c代码（因为这个函数本来就是c编译出来的，而且看它的汇编代码也很明
显看出痕迹：进入函数后保护函数内要用的寄存器和lr，然后全程没用r0~r3做中间变量，然后最后返回时，直接从栈中弹出lr到pc中）
int32_t FuncTable[] @"SHT$$INIT_ARRAY$$Base" = 
{
	0xfffc8161,
	0xfffc81a1,
	0xfffc838d,
	0xfffc83bd,
	0xfffc8425,
	0xfffc8d3d,
	0xfffc9109,
	0xfffc9481,
	0xfffc95fd,
	0xfffc96fd,
	0xfffc9881,
	0xfffc9c29,
	0xfffc9c91, //main.c中的__sta__dyninit()的相对与"此处"的偏移
};
void __cpp_initialize__aeabi_(void)
{
	int32_t start;
	int32_t end;
	void (*func)(void);
	
	//由于Cortex-M的流水线机制，这里的pc实际为当前执行指令+4
	start = 0x00035ae0 + 0x000025a0; //=0x00038080  //r4
									 //=&FuncTable
	end   = 0x00035b10 + 0x000025a4; //=0x000380b4  //r5
									 //=&FuncTable + sizeof(FuncTable)
	while(1)
	{
		if(start == end)
		{
			break;
		}
		else
		{
			func  = *start;
			func += start; //FuncTable中存放的实际是偏移，相对与FuncTable中相应元素的偏移
			func();
			
			start += 4;
		}
	}
}
不妨来计算以下：
.init_array
    0x000380b0:    fffc9c91    ....    DCD    4294745233	;;注意，这个就是我的main函数中的需要动态加载的数据
都是补码，直接相加即可：0xfffc9c91 + 0x000380b0 = 0x00001d41（发现就是__sta__dyninit()的首地址，只不过bit[0]=1）
关于bit[0]=0/1的问题，熟悉ARM的指令集的很容易明白。我这里随便找了一篇博文，不明白的可以看看：
https://blog.csdn.net/xinianbuxiu/article/details/52718178
d. 以上就完成了ram的动态加载，总结来说，就是函数的实际地址只有在运行期间才会确定，因此只能在运行后对这些变量来赋值。
e. 然后我们在main函数里使用这个变量，就很简单：加上r9这个基地址去使用就可以了。
f. 关于动态加载，在地址无关编译这节会详细说明，这里只是介绍一个简单流程，知道所有的数据可以被有理有据的被分配空间和初始化
就可以了，然后知道在main函数执行前完成了那些工作就可以了。
```

### 2.分散加载机制总结  

原本分散加载指的仅仅是`__scatter()`，而我习惯把动态加载也算在分散加载范畴，因为它们都是对ram进行初始化操作嘛。无论怎么划分都无所谓，大家知道这个过程就可以。  

|C语言中的变量类型|编译后的位置|分散加载器的操作|备注|
|:-:|:-:|:-:|:-:|:-:|
|局部变量|×|×|局部变量是分配在栈中的
|栈|.bss|__scatterload_zeroinit()||
|堆|.bss|__scatterload_zeroinit()||
|全局变量|.data|不一定|根据不同要求操作不同|
|未初始化的全局变量|.bss|__scatterload_zeroinit()||
|初始化但初始值全0|.bss|__scatterload_zeroinit()||
|初始化但初始值非全0|.data|__decompress()|只能说KEIL是这样，其他的我没有分析，不做定论|
|初始化但初始值各种各样|.data|__scatterload_copy()|如果使用地址无关编译，这个另说|
|const修饰的全局变量|.ro|×|直接分配在flash或者rom中，直接使用|
|static修饰的变量|.data/.bss|参考上面对.data/.bss的相关操作|在C语言中，static本质只是对编译器检查服务的，本质和全局变量没什么区别|

**特别说明：**

- KEIL通过分散加载文件进行配置，一般一次分配rw、zi、heap、stack（其中zi+heap+stack=.bss），之所以stack在heap后，是因为arm中栈一般是向下生长的，而heap又不是每次都能用完且又是相上生长，所以heap和stack可以做到最大程度的共用二者的空间。  
- 以上都是分析KEIL的情况，IAR和GCC我就没有特别分析，本质都是一样的，这些很底层的东西，很难有天翻地覆的变化，大家如果有兴趣可以自己分析。  
- 以上只是分析了没有使用地址无关编译时的分散加载过程（虽然我的示例代码的确使用了地址无关编译，本质没有区别的；同时这样也好，大家可以自己动手实践一遍，增强理解）。  
  
## 三、地址无关编译  

关于地址无关编译，我之前写了两篇随笔，简单介绍了一下：  

[[随笔]ELF文件、编译/链接、静态链接、动态链接](https://blog.csdn.net/weixin_39869569/article/details/104041494).  
[[随笔]C语言动态加载、rwpi和ropi、lwp轻量型进程初步解读](https://blog.csdn.net/weixin_39869569/article/details/104041494).  

本篇文章，主要用实例介绍一下`ropi`和`rwpi`，然后解释一下为什么使用地址无关编译之后，无法使用const的函数指针数组问题，以及如果非要这么用的一个曲线救国的方法。  

### 1.ropi介绍  

主要是针对`const变量`和`由分散加载器初始化的数据`（放在flash中的内容，一般不包括函数）。  
官方解释：  

```c
ROPI = Read-Only Position Independence. This concerns everything that is readonly in the ELF output from the linker. Note that this includes const data and data initializers, i.e. typically everything that is put in FLASH.

主要说明了以下3点：
1. const data也就是const修饰的变量；
2. data initializers也就是数据初始化器（我理解的是为动态加载器__sta__dyninit()）；
3. 函数；
以上3点即包括了在flash中的所有数据（everything that is put in FLASH）。
```

所以裸机下没有任何问题，因为裸机下我们一般要指定我们的程序运行在flash的哪个位置（简单的像是.hex文件，每条记录都是带有地址的，这就是为什么.hex可以转化为.bin文件，反过来大概率是不行的，原因就是`.bin文件纯指令代码，没有地址信息。类似于水合成石油，这辈子是不可能的，因为水里面没有碳元素。核聚变吧，聚一聚就出来碳元素了`）。  
而加上操作系统加持之后就不一样了，程序在那运行，程序员说的不算，操作系统说的算。而且一般都会把程序放到一些类似磁盘啊、外部flash等存储介质中，只有使用到时才拿出来，要么放到内存中运行，要么放到内部flash中运行。（当然你如果非要说linux下可以指定一些.so库的运行位置，那我无话可说，那本质也是os指定每次都把这个.so库放到同一个位置）。因此必须引入地址无关编译，把地址分配的权利交给os来分配。  
实现ropi的前提是：**程序块中的函数、变量位置，彼此之间是相对的**，否则一切免谈。  
也不是免谈，是那种情况一时半会我也解释不清，是我太菜了（有兴趣可以搜索.got和.plt）。  

**我们只分析基于这种前提下的ropi：**  

老习惯，列代码：  

#### a.const data的处理  

```c
//1.c语言中的代码
const long TestData[4] = {1, 2, 3, 4};
int main( void )
{
	return (int)TestData[0];
}

//2.以下是对应的汇编代码
i.main
$Super$$main
    0x000062cc:    4801        .H      LDR      r0,[pc,#4] ; [0x62d4] = 0x5456
    0x000062ce:    4478        xD      ADD      r0,r0,pc
    0x000062d0:    6800        .h      LDR      r0,[r0,#0]
    0x000062d2:    4770        pG      BX       lr
$d
    0x000062d4:    00005456    VT..    DCD    21590
;;省略其他代码

//3.const数据在flash中的位置
.constdata
TestData
    0x0000b728:    00000001    ....    DCD    1
    0x0000b72c:    00000002    ....    DCD    2
    0x0000b730:    00000003    ....    DCD    3
    0x0000b734:    00000004    ....    DCD    4
```

通过分析会发现：`0x000062cc`~`0x000062d0`对应的访问`TestData[0]`的操作。  
发现借助了`pc`和`$d`这个表，通过计算发现`0x000062ce`行执行完`r0`的值为`0x5456 + pc = 0x0000b728`，对应的`Testdata[0]`。  

**可以发现，这个偏移是相对与调用处的那条指令而言的，而不是`$d`表的首地址或者其他的什么。**  

#### b.data initializers的处理  

```c
.text
__sta__dyninit
    0x00001d40:    4803        .H      LDR      r0,[pc,#12] ; [0x1d50] = 0x21ac3
    0x00001d42:    4478        xD      ADD      r0,r0,pc
    0x00001d44:    4903        .I      LDR      r1,[pc,#12] ; [0x1d54] = 0x1b0c
    0x00001d46:    4449        ID      ADD      r1,r1,r9
    0x00001d48:    6008        .`      STR      r0,[r1,#0]
    0x00001d4a:    6048        H`      STR      r0,[r1,#4]
    0x00001d4c:    4770        pG      BX       lr
$d
    0x00001d4e:    0000        ..      DCW    0
    0x00001d50:    00021ac3    ....    DCD    137923
    0x00001d54:    00001b0c    ....    DCD    6924

;;省略其他代码

i.StartThread_Entry
StartThread_Entry
    0x00023808:    b510        ..      PUSH     {r4,lr}
    0x0002380a:    4604        .F      MOV      r4,r0
    0x0002380c:    f7e5fcaa    ....    BL       CheckSysLimit ; 0x9164
```

通过分析会发现：`0x00001d40`~`0x00001d42`对应的获取`StartThread_Entry()函数的起始地址`的操作。  
发现借助了`pc`和`$d`这个表，通过计算发现`0x00001d42`行执行完`r0`的值为`0x21ac3 + pc = 0x00023809`，对应的`StartThread_Entry()起始地址 + bit[0]=1`。  

#### c.函数的调用  

```c
//1.调用库里面的函数示例
	0x000308d6:    a023        #.      ADR      r0,{pc}+0x8e ; 0x30964
	0x000308d8:    f7d1fae4    ....    BL       __2printf ; 0x1ea4

//2.调用用户自定义程序示例
	0x000242c8:    f004fa04    ....    BL       api_InitSysWatchVariable ; 0x286d4
	0x000242cc:    f004f9b0    ....    BL       api_InitLcdVariable ; 0x28630

//3.被调用函数的地址
i.api_InitLcdVariable
api_InitLcdVariable
    0x00028630:    2001        .       MOVS     r0,#1
;;省略其他代码
```

可以发现无论调用库函数还是自定义的函数，都不会使用`pc`来作为基地址（没有额外的±`pc`）。  
但实际上，**BL指令本身就是一个相对跳转**，计算方法如下：  

首先这个BL是一个Thumb指令，是2个字节的，虽然长的4个字节，很容易被误解，其实这个长跳转就是两个跳转指令组合成的格式如下：  

|bit[15]|bit[14]|bit[11]|bit[12]|[bit11]|bit[10:0]|
|:-:|:-:|:-:|:-:|:-:|:-:|
|1|1|1|1|0/1|地址偏移|
|||||0，偏移高位||
|||||1，偏移低位||

所以：  

1. 每两个字节为一组，判断bit[11]：bit[11]=0的为高偏移/bit[11]=1的为低偏移；  
2. 去掉去掉高5位；  
3. 高偏移左移12位，低偏移左移1位，然后相加；  
4. 加上BL指令所在的pc，然后加4；  
5. 完毕；  

```c
则对于下述指令：
0x000242cc:    f004f9b0    ....    BL       api_InitLcdVariable ; 0x28630

1.先判断指令中的地址偏移字段
0xf004, bit[11]=0, 为地址高位偏移 
0xf9b0, bit[11]=1, 为地址低位偏移
2.各自取低11位
0xf004 -> 0x004
0xf9b0 -> 0x1b0
3.拼接
0x004<<12 +  0x1b0<<1 = 0x004000 + 0x360 = 0x004360
4.加上指令所在的偏移
0x004360 + (0x000242cc + 4) = 0x28630(为api_InitLcdVariable 函数的地址)
```

**总结：函数调用本身就是使用的地址无关操作，而ro和动态加载的操作通过加入pc偏移来实现。**  

### 2.rwpi介绍  

rwpi就比较好理解了：对于rw的数据使用，或者直接说是ram中的数据的使用。  
因为运行时数据在ram中的位置也是不确定的，也是只有运行时才知道os把这块数据分配在ram空间的哪里。  
处理方式很简单，但os分配ram时把这块空间的基地址获取出来，保存在一个地方，使用的时候加上就可以了。  

同样，给一个官方说明：  

```c
RWPI = Read-Write Position Independence. This concerns everything that is readwrite in the ELF output from the linker.

everything that is readwrite无非就是包括以下几个方面：
1. rw数据
2. zi数据
```

首先rwpi的机制是：  
- 编译时把所有变量的访问改成`r9`+`偏移`的方式，这个偏移在编译的时候给定0，然后很自然得到每个变量的偏移地址了。
- 运行时，os先分配一块空间，把基地址保存到`r9`中，然后再执行分散加载的那一套操作。  
  
#### a.rw数据的处理  

#### b.zi数据的处理  

统一用一个程序来作为示例：  

```c
//1.c语言中的代码
long TestData1[4] = {1, 2, 3, 4};
long TestData2[4];

int main( void )
{
	long dump = TestData2[0] + TestData1[0];
	return (int)dump;
}

//2.以下是对应的汇编代码
i.main
$Super$$main
    0x000062cc:    4903        .I      LDR      r1,[pc,#12] ; [0x62dc] = 0x3640
    0x000062ce:    4449        ID      ADD      r1,r1,r9
    0x000062d0:    6809        .h      LDR      r1,[r1,#0]
    0x000062d2:    4a03        .J      LDR      r2,[pc,#12] ; [0x62e0] = 0x1a48
    0x000062d4:    444a        JD      ADD      r2,r2,r9
    0x000062d6:    6812        .h      LDR      r2,[r2,#0]
    0x000062d8:    1888        ..      ADDS     r0,r1,r2
    0x000062da:    4770        pG      BX       lr
$d
    0x000062dc:    00003640    @6..    DCD    13888
    0x000062e0:    00001a48    H...    DCD    6728
```

在对代码分析的时候，`r9`给定0，然后结合.map文件进行分析。  
可以得出： 
- `0x000062cc`~`0x000062d0`行对应的是访问ram空间地址`r9`+`0x00003640`的地址，对应的TestData2[0]，这个地址很明显在.bss里；  
- `0x000062d2`~`0x000062d6`行对应的是访问ram空间地址`r9`+`0x00001a48`的地址，对应的TestData1[0]，这个地址在.data中；  
  
```c
//.map文件
TestData1		0x0000d600   Data          16  main.o(.data)

//加载视图和运行视图的对应关系
		0x0000bb67:    XXXXXXXX    ....    DCD    XXXXXX ;;bin文件中的base地址	
Region$$Table$$Base
		0x0000bb68:    00000051    Q...    DCD    81
		0x0000bb6c:    00000002    ....    DCD    2
		0x0000bb70:    00001a74    t...    DCD    6772
		0x0000bb74:    0000ba6b    k...    DCD    47723

//很容易得出.data在.bin中位置
** Section #3 'data' (SHT_PROGBITS) [SHF_ALLOC + SHF_WRITE]
    Size   : 1992 bytes (alignment 4)
    Address: 0x0000bbb8

//然后做转换运算
用.map文件中TestData1对应的0x0000d600 - 0x0000bbb8 = 0x1a48
这个值 = &TestData1[0]; //赋初始值的全局变量
```

### 3.为什么使用地址无关编译之后，无法使用const的函数指针数组  

根据前面的示例，不难看出，无论是`rwpi`还是`ropi`，都只是对变量访问的一些处理，仅仅是在访问时通过给定一个基地址，然后通过加上偏移地址的方式来使用。  这个基地址分为两种：ram空间的用`r9`，flash空间的用`pc`。除此之外，没有做其他的处理。  

然后我们来分析：  
- 在编译阶段，完成的是对每一个文件的翻译工作（根据情况分配到code、ro、rw、zi中，形成不同的段）。  
	- 对于外部的符号，只要由声明的操作，则会在每一个函数的汇编代码后面生成一个重定位表（前面见到的`$d`表），将指令中的调用指向这个重定位表的每一项；同时还要生成一个重定位表的段，用于在链接阶段进行重定位时作为参考。  
- 链接的初期，将所有文件的相同的段形成一个整体的段（即所有的rw段整合成一个rw段...）。  
- 链接的后期，根据重定位段对编译阶段形成的那个`$d`进行修改。  
  

而当使用了const的函数指针数组时，这个数组显然要放到flash中，正常说编译阶段是可以通过的（假设说只根据编译阶段完成的任务来判断，而不是实际编译器的操作），这时候不知道数组的初始值，编译器生成一个重定位表同时用个临时数据占位，这都是可行的。  
然后是链接的初期，整合所有的段形成一个段，这一步也是没问题的，因为就是一个拼接而已。  
最后是链接的后期，就是重定位阶段，理论上也是没有问题，访问普通的变量是没有问题的，但对于访问其中的函数指针来说这一步有些小问题：  
- 在地址无关编译操作下，根据`ropi`的处理流程，所有的对const变量使用是`pc+偏移`的操作来实现访问的。  
- 所以使用这个const变量的那块代码肯定是可以正确访问到的，但是访问到的函数指针这个值就不是正确的了，因为重定位进行的时候这个函数指针的值不是实际运行的时候的那个，而flash又不能想ram那样动态加载，所以无法通过`pc`来作为基地址来相对访问。  
- 但是ram中（非const修饰的全局变量）可以使用函数指针，通过动态加载把函数的**绝对地址**赋给了函数指针（这个可以在本篇博客中搜一下`__sta__dyninit()`，一目了然）。  
  

所以造成这个问题的原因就是flash的特殊性：不可改（起码是不是像ram那样的可修改，如果你偏要说flash可擦写，我也没办法。我的意思是，flash终究是flash，ram终究是ram，二者虽然一些性质相似，但终究是两种东西，否则还区分个什么劲）。  

 **总之，在地址无关编译时，const变量中不能出现在只有在运行时才能确定数据！如将函数指针和变量的地址赋给const变量！这个变量的地址包括ro、rw和zi。**  

### 4."治标不治本"的解决方法  

所以编译器根本就不会允许const函数指针数组这种语法出现，直接报错了，所以无能为力。所有的调用处没有特殊处理，即这个变量里面的是绝对地址，调用处直接使用，根本不会再加上`pc`（除非我们在使用处人为的去获取`pc`，但这个的前提还得是能通过编译器的编译，然后链接器链接上一个相对于某个已知位置的相对偏移）。  
所以，目前只能仿照ram的动态加载机制，改写const函数指针数组，通过一个辅助函数来获取函数指针（利用了函数调用本身就是使用相对地址的这一特性）。  

```c
void test1(char a) {
    printf("this is dunc:test1 %c\n", a);
}
void test2(char a) {
    printf("this is dunc:test2 %c\n", a);
}
void test3(char a) {
    printf("this is dunc:test3 %c\n", a);
}

typedef struct
{
	char 	c;
	int 	f;
}_T_Test;

const _T_Test test[3] = 
{
	{	'a',	1, },
	{	'b', 	2, },
	{	'c', 	3, },
};

typedef void (*Func)(char);
Func Select(int idx)
{
	switch(idx)
	{
		case 1:
			return test1;
		case 2:
			return test2;
		case 3:
			return test3;
		default:
			return 0;
	}
}

int main(void)
{
	Select(test[0].f)(test[0].c);
	Select(test[1].f)(test[1].c);
	Select(test[2].f)(test[2].c);
    
    return 0;
}
```

如果大家有什么更好的方法，欢迎一起交流。  

## 四、总结  

1. 分散加载机制，本质就是从flash或rom等非易失存储介质中把数据“搬移”到ram中，不同的是对于不同类型的数据会进行不同的处理，甚至有的还需要动态加载。  
2. 动态加载时，离不开os（注意单片机上的os毕竟不是windows上的os，能力有限，但原理相通）。  
3. 使用地址无关编译，就是使用`ropi`和`rwpi`两种技术，其本质是对变量进行地址无关的处理（因为函数天生就是地址无关的），从而引进了对ro的处理，对rw和zi的处理，甚至是对函数指针数组的处理，原理就是利用相对寻址，利用代码和数据本身的相对地址不变这一特性来实现的。  

## 五、不使用地址无关编译时的分散加载分析（示例代码）  

最后简单列以下不使用地址无关编译时的分散加载分析，大家有兴趣可以简单分析以下：  

```c
!!!main
__main
    0x080101ac:    f000f802    ....    BL       __scatterload ; 0x80101b4
    0x080101b0:    f000f8c9    ....    BL       __rt_entry ; 0x8010346
!!!scatter
__scatterload
__scatterload_rt2
__scatterload_rt2_thumb_only
    0x080101b4:    a00a        ..      ADR      r0,{pc}+0x2c ; 0x80101e0
    0x080101b6:    e8900c00    ....    LDM      r0,{r10,r11}
    0x080101ba:    4482        .D      ADD      r10,r10,r0
    0x080101bc:    4483        .D      ADD      r11,r11,r0
    0x080101be:    f1aa0701    ....    SUB      r7,r10,#1
__scatterload_null
    0x080101c2:    45da        .E      CMP      r10,r11
    0x080101c4:    d101        ..      BNE      0x80101ca ; __scatterload_null + 8
    0x080101c6:    f000f8be    ....    BL       __rt_entry ; 0x8010346
    0x080101ca:    f2af0e09    ....    ADR      lr,{pc}-7 ; 0x80101c3
    0x080101ce:    e8ba000f    ....    LDM      r10!,{r0-r3}
    0x080101d2:    f0130f01    ....    TST      r3,#1
    0x080101d6:    bf18        ..      IT       NE
    0x080101d8:    1afb        ..      SUBNE    r3,r7,r3
    0x080101da:    f0430301    C...    ORR      r3,r3,#1
    0x080101de:    4718        .G      BX       r3
$d
    0x080101e0:    000258e0    .X..    DCD    153824
    0x080101e4:    00025930    0Y..    DCD    153904
$t
!!dczerorl2
__decompress
__decompress1
    0x080101e8:    440a        .D      ADD      r2,r2,r1
    0x080101ea:    f04f0c00    O...    MOV      r12,#0
    0x080101ee:    f8103b01    ...;    LDRB     r3,[r0],#1
    0x080101f2:    f0130407    ....    ANDS     r4,r3,#7
    0x080101f6:    bf08        ..      IT       EQ
    0x080101f8:    f8104b01    ...K    LDRBEQ   r4,[r0],#1
    0x080101fc:    111d        ..      ASRS     r5,r3,#4
    0x080101fe:    bf08        ..      IT       EQ
    0x08010200:    f8105b01    ...[    LDRBEQ   r5,[r0],#1
    0x08010204:    1e64        d.      SUBS     r4,r4,#1
    0x08010206:    d005        ..      BEQ      0x8010214 ; __decompress + 44
    0x08010208:    f8106b01    ...k    LDRB     r6,[r0],#1
    0x0801020c:    1e64        d.      SUBS     r4,r4,#1
    0x0801020e:    f8016b01    ...k    STRB     r6,[r1],#1
    0x08010212:    d1f9        ..      BNE      0x8010208 ; __decompress + 32
    0x08010214:    f0130f08    ....    TST      r3,#8
    0x08010218:    bf1e        ..      ITTT     NE
    0x0801021a:    f8104b01    ...K    LDRBNE   r4,[r0],#1
    0x0801021e:    1cad        ..      ADDNE    r5,r5,#2
    0x08010220:    1b0c        ..      SUBNE    r4,r1,r4
    0x08010222:    d109        ..      BNE      0x8010238 ; __decompress + 80
    0x08010224:    1e6d        m.      SUBS     r5,r5,#1
    0x08010226:    bf58        X.      IT       PL
    0x08010228:    f801cb01    ....    STRBPL   r12,[r1],#1
    0x0801022c:    d5fa        ..      BPL      0x8010224 ; __decompress + 60
    0x0801022e:    e005        ..      B        0x801023c ; __decompress + 84
    0x08010230:    f8146b01    ...k    LDRB     r6,[r4],#1
    0x08010234:    f8016b01    ...k    STRB     r6,[r1],#1
    0x08010238:    1e6d        m.      SUBS     r5,r5,#1
    0x0801023a:    d5f9        ..      BPL      0x8010230 ; __decompress + 72
    0x0801023c:    4291        .B      CMP      r1,r2
    0x0801023e:    d3d6        ..      BCC      0x80101ee ; __decompress + 6
    0x08010240:    4770        pG      BX       lr
    0x08010242:    0000        ..      MOVS     r0,r0
!!handler_copy
__scatterload_copy
    0x08010244:    3a10        .:      SUBS     r2,r2,#0x10
    0x08010246:    bf24        $.      ITT      CS
    0x08010248:    c878        x.      LDMCS    r0!,{r3-r6}
    0x0801024a:    c178        x.      STMCS    r1!,{r3-r6}
    0x0801024c:    d8fa        ..      BHI      __scatterload_copy ; 0x8010244
    0x0801024e:    0752        R.      LSLS     r2,r2,#29
    0x08010250:    bf24        $.      ITT      CS
    0x08010252:    c830        0.      LDMCS    r0!,{r4,r5}
    0x08010254:    c130        0.      STMCS    r1!,{r4,r5}
    0x08010256:    bf44        D.      ITT      MI
    0x08010258:    6804        .h      LDRMI    r4,[r0,#0]
    0x0801025a:    600c        .`      STRMI    r4,[r1,#0]
    0x0801025c:    4770        pG      BX       lr
    0x0801025e:    0000        ..      MOVS     r0,r0
!!handler_zi
__scatterload_zeroinit
    0x08010260:    2300        .#      MOVS     r3,#0
    0x08010262:    2400        .$      MOVS     r4,#0
    0x08010264:    2500        .%      MOVS     r5,#0
    0x08010266:    2600        .&      MOVS     r6,#0
    0x08010268:    3a10        .:      SUBS     r2,r2,#0x10
    0x0801026a:    bf28        (.      IT       CS
    0x0801026c:    c178        x.      STMCS    r1!,{r3-r6}
    0x0801026e:    d8fb        ..      BHI      0x8010268 ; __scatterload_zeroinit + 8
    0x08010270:    0752        R.      LSLS     r2,r2,#29
    0x08010272:    bf28        (.      IT       CS
    0x08010274:    c130        0.      STMCS    r1!,{r4,r5}
    0x08010276:    bf48        H.      IT       MI
    0x08010278:    600b        .`      STRMI    r3,[r1,#0]
    0x0801027a:    4770        pG      BX       lr

;;省略无关代码 

Region$$Table$$Base
        0x08035ac0:    08035dbc    .]..    DCD    134438332
        0x08035ac4:    20000000    ...     DCD    536870912
        0x08035ac8:    00000010    ....    DCD    16
        0x08035acc:    08010244    D...    DCD    134283844
        0x08035ad0:    08035dcc    .]..    DCD    134438348
        0x08035ad4:    20000080    ...     DCD    536871040
        0x08035ad8:    000003cc    ....    DCD    972
        0x08035adc:    080101e8    ....    DCD    134283752
        0x08035ae0:    08035f10    ._..    DCD    134438672
        0x08035ae4:    10000000    ....    DCD    268435456
        0x08035ae8:    00000318    ....    DCD    792
        0x08035aec:    080101e8    ....    DCD    134283752
        0x08035af0:    08035f10    ._..    DCD    134438672
        0x08035af4:    2000044c    L..     DCD    536872012
        0x08035af8:    000018d4    ....    DCD    6356
        0x08035afc:    08010260    `...    DCD    134283872
        0x08035b00:    08036058    X`..    DCD    134439000
        0x08035b04:    10000318    ....    DCD    268436248
        0x08035b08:    00008248    H...    DCD    33352
        0x08035b0c:    08010260    `...    DCD    134283872

;;省略无关代码

** Section #2 'RO_IRAM0' (SHT_PROGBITS) [SHF_ALLOC + SHF_EXECINSTR]
    Size   : 16 bytes (alignment 4)
    Address: 0x20000000

    $t
    shm_code
    $v0
    lwp_sigreturn
        0x20000000:    f04f07fe    O...    MOV      r7,#0xfe
        0x20000004:    df00        ..      SVC      #0x0 ; formerly SWI
        0x20000006:    bf00        ..      NOP      
    lwp_thread_return
        0x20000008:    f04f0701    O...    MOV      r7,#1
        0x2000000c:    df00        ..      SVC      #0x0 ; formerly SWI
        0x2000000e:    bf00        ..      NOP      

** Section #3 'RW_IRAM1' (SHT_PROGBITS) [SHF_ALLOC + SHF_WRITE]
    Size   : 324 bytes (alignment 4)
    Address: 0x20000080

** Section #4 'RW_IRAM1' (SHT_NOBITS) [SHF_ALLOC + SHF_WRITE]
    Size   : 6356 bytes (alignment 8)
    Address: 0x2000044c

** Section #5 'RW_IRAM2' (SHT_PROGBITS) [SHF_ALLOC + SHF_WRITE]
    Size   : 328 bytes (alignment 4)
    Address: 0x10000000

** Section #6 'RW_IRAM2' (SHT_NOBITS) [SHF_ALLOC + SHF_WRITE]
    Size   : 33352 bytes (alignment 4)
    Address: 0x10000318

//.map文件
	Code (inc. 	data)  	   RO Data     RW Data   ZI Data  Debug   
    139584      29652      15500       1764      39708    1319746   Grand Totals
    139584      29652      15500        652      39708    1319746   ELF Image Totals (compressed)
    139584      29652      15500        652          0          0   ROM Totals
```